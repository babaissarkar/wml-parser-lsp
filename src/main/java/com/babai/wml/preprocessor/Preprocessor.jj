options
{
	LOOKAHEAD=3;
	STATIC=false;
	// Enable these to print verbose javacc logs
	DEBUG_PARSER = false;
	DEBUG_LOOKAHEAD = false;
	DEBUG_TOKEN_MANAGER = false;
}

PARSER_BEGIN(Preprocessor)
package com.babai.wml.preprocessor;

import java.awt.Color;
import java.util.*;
import java.util.logging.Logger;
import java.io.*;
import java.nio.file.Files;
import java.nio.file.Path;

import com.babai.wml.core.*;

import static com.babai.wml.utils.ANSIFormatter.*;

public class Preprocessor {
	private final Logger pL = Logger.getLogger("preprocessor.parse");
	private boolean showParseLogs = false;
	private boolean warnParseLogs = false;

	public final Color tdColor = new Color(255, 221, 0);
	public final Color macroNameColor = new Color(0, 255, 128);
	public final Color lineNumColor = new Color(0, 153, 255);
	public final Color filePathColor = new Color(128, 192, 255);
	public final Color tagColor = Color.decode("#00CED1");

	private Path currentPath = Path.of(".");
	private PrintStream out;
	private HashMap<String, Definition> defines = new HashMap<>();

	public Preprocessor(Path filePath) throws IOException {
		this(Files.newInputStream(filePath));
		this.currentPath = filePath;
	}

	/**
	 * Create a preprocessor subprocess, that inherits properties
	 * from this one.
	 */
	private Preprocessor child(Path inpath) throws IOException {
		Preprocessor pinc = new Preprocessor(inpath);
		pinc.showParseLogs = showParseLogs;
		pinc.warnParseLogs = warnParseLogs;
		pinc.out = (getOutput() == null) ? System.out : getOutput();
		pinc.token_source.dataPath = token_source.dataPath;
		pinc.token_source.userDataPath = token_source.userDataPath;
		pinc.token_source.showLogs = token_source.showLogs;
		return pinc;
	}

	/**
	 * Recursively parse the given Path p
	 * Note: JavaCC doesn't support lambdas or :: yet!
	 */
	public void subparse(Path p) throws IOException, ParseException {
		Path path = p;
		if (Files.isDirectory(path)) {
			Path p2 = path.resolve("./_main.cfg");
			if (!Files.exists(p2)) {
				File[] files = path.toFile().listFiles();
				try {
					if (files == null) return;
					for (File f : files) {
						if (!f.isDirectory() && !f.getName().endsWith(".cfg")) return;
						subparse(f.toPath());
					}
					return;
				} catch (IOException e) {
					e.printStackTrace();
				}
			} else {
				path = p2;
			}
		}

		debugPrint("Including: " + colorify(path.toString(), filePathColor));
		Preprocessor pp = child(path);
		pp.parse();
		addDefines(pp.getDefines());
	}

	/**
	 * Handwritten macro exapansion method
	 */
	private final String expandMacroCall(Vector<String> possibleArgs) throws ParseException {
		Token name = null;
		var args = new Vector<String>();
		var defArgs = new HashMap<String, String>();

		var sb = new StringBuffer();
		sb.append(token.image);

		if (token.kind == LBR) {
			token = getNextToken();
			if (skipWhitespace()) {
				sb.append(" ");
			}

			// macro name. must be uppercase + digits.
			if (token.kind == STRING) {
				String tokName = token.image;
				if (tokName.equals(tokName.toUpperCase())) {
					name = token;
				}
			} else if (token.kind == RBR) {
				//case {<whitespace>}, the ending brace
				sb.append("}");
				token = getNextToken();
				return sb.toString();
			}

			sb.append(token.image);
			token = getNextToken();

			// arguments to macro call
			while (token.kind != RBR) {
				if (skipWhitespace()) {
					sb.append(" ");
				}
				
				// keyword argument handling
				String key = null, val = null;
				if (token.kind == STRING && getToken(1).kind == EQL) {
					key = token.image;
					// skip this and next token
					token = getNextToken();
					token = getNextToken();
				}
				
				if (token.kind == LBR && name != null) {
					val = expandMacroCall(possibleArgs);
					sb.append(val);
				} else if (token.kind == EOF) {
					generateParseException();
				} else {
					if (name != null) {
						val = token.kind == INBLK
							? token.image.substring(1, token.image.length() - 1)
							: token.image;
					}

					sb.append(token.image);
					token = getNextToken();
				}
				
				if (key != null) {
					defArgs.put(key, val);
				} else {
					args.add(val);
				}
			}

			// the ending brace
			sb.append(token.image);
			token = getNextToken();

			if (name == null) {
				return sb.toString();
			}

			// parsed name + args successfully, attempt expansion
			Definition def = defines.get(name.image);
			if (def != null) {
				String argsAsString = Definition.argsAsString(args, defArgs);
				debugPrint(
					"Expanding macro " + colorify(def.name(), macroNameColor)
					+ (!argsAsString.isEmpty()
						? (" with values: " + colorify(argsAsString, macroNameColor))
						: ""));
				try {
					return def.expand(args, defArgs);
				} catch (IllegalArgumentException e) {
					e.printStackTrace();
					return sb.toString();
				}
			} else if (possibleArgs.contains(name.image)) {
				// FIXME: do nothing for now. may need checks later.
				return sb.toString();
			} else {
				warningPrint(position(name) + " undefined macro " + name.image);
				return sb.toString();
			}
		}

		return sb.toString();
	}


	// skip whitespace tokens
	private final boolean skipWhitespace() {
		boolean skipped = false;
		while (token.kind == SPACE) {
			skipped = true;
			token = getNextToken();
		}
		return skipped;
	}

	private final void handleDirectiveOrComment() throws ParseException {
		Token t = getToken(1);
		switch (t.kind) {
		case DEFINE:
			define();
			break;
		case IFDEF:
			ifdef();
			break;
		case UNDEF:
			undef();
			break;
		case TEXTDOMAIN:
			textdomain();
			break;
		default:
			while (token.kind != EOL) {
				token = getNextToken();
			}
			break;
		}
	}

	public PrintStream getOutput() {
		return out;
	}

	public void setOutput(PrintStream out) {
		this.out = out;
	}

	public HashMap<String, Definition> getDefines() {
		return this.defines;
	}

	public void addDefines(HashMap<String, Definition> newDefines) {
		this.defines.putAll(newDefines);
	}
	
	public void showParseLogs(boolean showParseLogs) {
		this.showParseLogs = showParseLogs;
	}
	
	public void showWarnLogs(boolean showWarnLogs) {
		this.warnParseLogs = showWarnLogs;
	}

	public void debugPrint(String s) {
		if (showParseLogs) {
			pL.info(s);
		}
	}

	public void warningPrint(String s) {
		if (showParseLogs || warnParseLogs) {
			pL.warning(s);
		}
	}

	public String position(Token tok) {
		return position(tok.beginLine, tok.beginColumn);
	}

	public String position(int line, int col) {
		return colorify("(" + line + ":" + col + ")", lineNumColor);
	}
}

PARSER_END(Preprocessor)

TOKEN_MGR_DECLS : {
	private final Logger tL = Logger.getLogger("preprocessor.token");

	public boolean showLogs = false;
	public Path dataPath;
	public Path userDataPath;

	public void debugPrint(String s) {
		if (showLogs) {
			tL.info(s);
		}
	}
}

<DEFAULT> TOKEN : {
    < ANGLE_OPEN: "<<" > { SwitchTo(ANGLE); }
}

<ANGLE> MORE : { < ~[] > }   // gobbles characters, appends to token

<ANGLE> TOKEN : {
    < ANGLE_CLOSE: ">>" > {
        matchedToken.kind = STRING;
        SwitchTo(DEFAULT);
    }
}

TOKEN : {
	< ARG    : "#arg"   >
|	< ENDARG : "#endarg" >
}

TOKEN : {
	< DEFINE : "define" >	{ debugPrint("DEFINE"); }
|	< ENDDEF : "enddef" >	{ debugPrint("ENDDEF"); }
|	< IFDEF  : "ifdef" >	{ debugPrint("IFDEF"); }
|	< ENDIF  : "endif" >	{ debugPrint("ENDIF"); }
|	< UNDEF  : "undef" >	{ debugPrint("UNDEF"); }
|	< TEXTDOMAIN : "textdomain" > { debugPrint("TEXTDOMAIN"); }
|	< LBR    : "{" >		{ debugPrint("Left Brace"); }
|	< RBR    : "}" >		{ debugPrint("Right Brace"); }
|	< LSB    : "[" >		{ debugPrint("Left Sq Bkt"); }
|	< RSB    : "]" >		{ debugPrint("Right Sq Bkt"); }
|	< LPAR   : "(" >		{ debugPrint("Left Par"); }
|	< RPAR   : ")" >		{ debugPrint("Right Par"); }
|	< LANGLE : "<" >		{ debugPrint("Left Angle"); }
|	< RANGLE : ">" >		{ debugPrint("Right Angle"); }
|	< EQL    : "=" >		{ debugPrint("Equals"); }
|	< SLASH  : "/" >		{ debugPrint("Slash"); }
|	< HASH   : "#" >		{ debugPrint("Hash"); }
|	< SPACE  : (" "|"\t")+ >	{ debugPrint("Space"); }
|	< QUOTED_STRING: "\"" (~["\""])* "\"" > { debugPrint("quoted string"); }
|	< INBLK  : <LPAR> (~[")"])+ <RPAR> >
|	< STAG   : <LSB> (["a"-"z"]|<DIGIT>|"_")+ <RSB> > { debugPrint("Start tag"); }
|	< ETAG   : <LSB> <SLASH> (["a"-"z"]|<DIGIT>|"_")+ <RSB> > { debugPrint("End tag"); }
|	< PATH   : (<STRING>) (<SLASH>) (<STRING>|<SLASH>)+ >	{ debugPrint("Path"); }
|	< STRING : (<LETTER>|<DIGIT>)+ >						{ debugPrint("STRING"); }
|	< MARG   : (<LETTER>|<DIGIT>|<LSB>|<RSB>|<SLASH>)+ >	{ debugPrint("STRING"); } // yes, macro arguments can have newline!
|	< NUMBER : (<DIGIT>)+ ("." (<DIGIT>)+)? >				{ debugPrint("NUMBER"); }
|	< LETTER :
		["A"-"Z"]|["a"-"z"]|"?"|"!"|":"|";"|"."|","|"‘"|"’"|"%"
		|"_"|"-"|"'"|"+"|<LANGLE>|<RANGLE>|<LPAR>|<RPAR>|"~"|"*"|"^"|"\""
		|"“"|"”"|"―"|"—"|"|"|"\\"|"$"|"@"|"✘"|"✔"
	>
	{ debugPrint("LETTER"); }
|	< DIGIT  : ["0"-"9"] >					{ debugPrint("DIGIT"); }
|	< EOL    : "\n"|"\r"|"\r\n" >			{ debugPrint("EOL"); }
}


void parse() :
{
	Token tok; String content, exp;
}
{
	(
		content=tag() { out.print(content); }
		| expandPath()
		| <LBR> { out.print(expandMacroCall(new Vector<>())); }
		| (tok=<SPACE>|tok=<EOL>)   { out.print(tok.image); }
		| <HASH> { handleDirectiveOrComment(); }
		| <EOF> {
			out.println();
			debugPrint(defines.size() + " defines found.");
			out.close();
			return;
		}
	)+
}

String tag() :
{
	Token tstart, tend, body;
	String macro, contents;
	StringBuilder sb = new StringBuilder();
}
{
	(
		(tstart=<STAG>) { sb.append(tstart.image); }
		(<EOL>)?
		{
			// TODO can check tag mismatch here
			while (token.kind != ETAG) {
				if (token.kind == LBR) {
					// nested macro call
					sb.append(expandMacroCall(new Vector<String>()));
				} else if (token.kind == HASH) {
					// handleDirectiveOrComment(); // FIXME faulty
					token = getNextToken();
				} else if (getToken(1).kind == STAG) {
					// nested tag
					sb.append("\t" + tag());
					token = getNextToken();
				} else {
					// anything else except comments is just appended as string to result
					sb.append(token.image);
					token = getNextToken();
				}
			}

			debugPrint("Tag found: " + colorify(tstart.image, tagColor));
			sb.append(token.image);
		}
	) { return sb.toString(); }
}

void textdomain() :
{
	Token tok;
}
{
	<TEXTDOMAIN> <SPACE> (tok=<STRING>) { debugPrint("Textdomain " + colorify(tok.image, tdColor)); }
}

void define() :
{
	Token name, arg;
	Token key, val;
	StringBuilder sb = new StringBuilder();
	Definition def;
	String macro;
	var args = new Vector<String>();
	var defArgs = new HashMap<String, String>();
	Map.Entry<String, String> defArg;
}
{
	(
		<DEFINE> <SPACE>
		(name=<STRING>)
		(
			<SPACE> (arg=<STRING>) { args.add(arg.toString()); }
		)*
		(<EOL>)*
		(
			(<SPACE>)? (defArg=defaultArg(args)) {
				defArgs.put(defArg.getKey(), defArg.getValue());
			}
			(<EOL>)*
		)*
	) {
		while (token.kind != ENDDEF) {
			if (token.kind == LBR) {
				// nested macro call
				sb.append(expandMacroCall(args));
			} else if (token.kind == HASH) {
				// handleDirectiveOrComment(); // FIXME faulty
				token = getNextToken();
			} else {
				// anything else is just appended as string to result
				sb.append(token.image);
				token = getNextToken();
			}
		}

		def = new Definition(name.toString(), sb.toString(), args, defArgs);
		debugPrint("defining macro " + colorify(def.name(), macroNameColor));
		defines.put(name.toString(), def);
	}
}

Map.Entry<String, String> defaultArg(Vector<String> args) :
{
	Token argName, argBody;
}
{
	<ARG> <SPACE> argName=<STRING>
	(<SPACE>|<EOL>)+ {
		var sb = new StringBuffer();
		while (token.kind != ENDARG) {
			if (token.kind == LBR) {
				// nested macro call
				sb.append(expandMacroCall(args));
			} else if (token.kind == HASH) {
				// handleDirectiveOrComment(); // FIXME faulty
				token = getNextToken();
			} else {
				// anything else is just appended as string to result
				sb.append(token.image);
				token = getNextToken();
			}
		}
		if (sb.toString().startsWith("\n")) {
			sb.deleteCharAt(0);
		}
		return Map.entry(argName.image, sb.toString());
	}
}

String expand(Vector<String> possibleArgs) :
{
	Token name, arg;
	Vector<String> args = new Vector<>();
	StringBuilder sb = new StringBuilder();
	String macro;
}
{
	(
		(<LBR>) (<SPACE>)* (name=<STRING>) { sb.append("{" + name.image); }
		(
			<SPACE> (
				macro=expand(new Vector<>()) { sb.append(" " + macro); args.add(macro); }
			|	(arg=<STRING>|arg=<MARG>|arg=<PATH>) { sb.append(" " + arg.image); args.add(arg.toString()); }
			|	(arg=<INBLK>) {
				sb.append(" " + arg.image);
				args.add(arg.image.substring(1, arg.image.length()-1));
			} // fix for parens
			)
		)*
		(<SPACE>)*
		(<RBR>) { sb.append("}"); }
	) {
		Definition def = defines.get(name.image);
		if (def != null) {
			debugPrint("expanding macro " + def.name());
			try {
				return def.expand(args);
			} catch(IllegalArgumentException e) {
				e.printStackTrace();
				return sb.toString();
			}
		} else if (possibleArgs.contains(name.image)) {
			// FIXME: do nothing for now. may need checks later why this is happening.
			return sb.toString();
		} else {
			warningPrint(position(name) + " undefined macro " + name.image);
			return sb.toString();
		}
	}
}

void expandPath() :
{
	Token tok;
	StringBuilder sb = new StringBuilder();
}
{
	(<LBR> (tok=<PATH>) <RBR>) {
		Path p;
		if (tok.image.startsWith(".")) {
			if (Files.isDirectory(currentPath)) {
				p = currentPath.resolve(tok.image);
			} else {
				p = currentPath.getParent().resolve(tok.image);
			}
		} else if (tok.image.startsWith("~")) {
			// TODO should throw warning if userdatapath is null
			// Supports both ~add-ons and ~/add-ons
			String relpath = tok.image.substring(1);
			if (relpath.startsWith("/")) {
				relpath = relpath.substring(1);
			}
			p = token_source.userDataPath.resolve(relpath);
		} else {
			// TODO should throw warning if datapath is null
			p = token_source.dataPath.resolve(tok.image);
		}

		debugPrint("Trying to include: " + colorify(p.toString(), filePathColor));

		if (!Files.isDirectory(p) && !p.toString().endsWith(".cfg")) return;

		if (Files.exists(p)) {
			debugPrint("Including: " + colorify(p.toString(), filePathColor));
			try {
				subparse(p);
			} catch(IOException ioe) {
				debugPrint("Cannot parse " + tok.image);
				ioe.printStackTrace();
			}
		} else {
			warningPrint(tok.image + " not found");
		}
	}
}

void ifdef() :
{
	Token name;
}
{
	(<IFDEF> <SPACE> (name=<STRING>) <EOL>) {
		if (defines.get(name.image) == null) {
			debugPrint("ifdef: define " + name.image + " not found, skipping block");
			while (token.kind != ENDIF) {
				token = getNextToken();
			}
		} else {
			debugPrint("ifdef: define " + name.image + " found, parsing block");
		}
	}
}

void undef() :
{
	Token name;
}
{
	<UNDEF> <SPACE> (name=<STRING>) {
		debugPrint("removing macro " + name.toString());
		defines.remove(name.image);
	}

}


